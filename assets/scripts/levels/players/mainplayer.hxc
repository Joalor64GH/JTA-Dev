import flixel.FlxG;
import jta.input.Input;
import jta.objects.level.Player;
import jta.Paths;

class MainPlayer extends Player
{
	var jumping:Bool = false;
	var jumpTimer:Float = 0;

	public function new():Void
	{
		super('mainplayer');

		loadGraphic(Paths.image('player/player'), true, 16, 16);

		animation.add('idle', [0], 1);
		animation.add('walk', [1, 0, 2, 0], 12);
		animation.add('jump', [3], 1);
		animation.add('dead', [4], 1);

		animation.play('idle');

		characterHitbox.maxVelocity.y = 290;
		characterHitbox.acceleration.y = 900;

		initializeHitbox(12, 14);
	}

	public override function update(elapsed:Float):Void
	{
		if (characterControllable)
		{
			if (jumping && !Input.pressed('jump'))
				jumping = false;

			if (jumpTimer >= 0 && Input.pressed('jump') && characterHitbox.velocity.y == 0)
			{
				FlxG.sound.play(Paths.sound('jump'));
				jumpTimer += elapsed;
				jumping = true;
				if (jumpTimer > 0 && jumpTimer < 0.25)
					characterHitbox.velocity.y = -290;
			}
			else
			{
				jumpTimer = 0;
				jumping = false;
			}
		}

		super.update(elapsed);

		if (characterControllable)
		{
			if (velocity.y < 0)
				animation.play('jump');
			else if (velocity.x != 0)
				animation.play('walk');
			else
				animation.play('idle');

			if (Input.pressed('left'))
				characterHitbox.velocity.x = -150;
			else if (Input.pressed('right'))
				characterHitbox.velocity.x = 150;
			else
				characterHitbox.velocity.x = 0;

			if (velocity.x != 0)
				flipX = velocity.x < 0;

			if (Input.pressed('run'))
			{
				if (Input.pressed('left'))
					characterHitbox.velocity.x -= 50;
				else if (Input.pressed('right'))
					characterHitbox.velocity.x += 50;
			}

			#if debug
			if (FlxG.keys.justPressed.F4)
				trace('player position:' + x + ', ' + y);
			#end
		}
	}
}
